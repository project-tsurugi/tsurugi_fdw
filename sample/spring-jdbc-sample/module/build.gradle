plugins {
    id 'application'
}

repositories {
    mavenCentral()      // Maven Central リポジトリ（依存関係をダウンロードするためのリポジトリ）
}

dependencies {
    implementation 'org.springframework:spring-jdbc:6.2.8'      // Spring Framework（Spring JDBC）を利用するためのドライバ
    implementation 'org.postgresql:postgresql:42.7.3'           // PostgreSQL データベースに接続するためのドライバ
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)        // Java ツールチェーン: Java 17 を使用
    }
}

application {
    mainClass = 'com.tsurugidb.fdw.spring.jdbc.sample.SpringJdbcSample'     // メインクラス: アプリケーションのエントリーポイント
}

task createTable(type: Exec) {
    // Tsurugi データベースに fdw_sample テーブルを作成するタスク
    commandLine 'sh', '-c', 'tgsql --exec -c ipc:tsurugi "create table if not exists fdw_sample (num int not null, tim time)"'
}

task showTable(type: Exec) {
    // Tsurugi データベースに作成した fdw_sample テーブルを確認するタスク
    commandLine 'sh', '-c', 'tgsql --exec -c ipc:tsurugi "\\show table fdw_sample"'
}

task dropTable(type: Exec) {
    // Tsurugi データベースから fdw_sample テーブルを削除するタスク
    commandLine 'sh', '-c', 'tgsql --exec -c ipc:tsurugi "drop table if exists fdw_sample"'
}

task createForeignTable(type: Exec) {
    // PostgreSQL データベースに fdw_sample 外部テーブルを作成するタスク
    commandLine 'sh', '-c', 'psql postgres -c "create foreign table if not exists fdw_sample (num int not null, tim time) server tsurugi"'
}

task showForeignTable(type: Exec) {
    // PostgreSQL データベースに作成した fdw_sample 外部テーブルを確認するタスク
    commandLine 'sh', '-c', 'psql postgres -c "\\d fdw_sample"'
}

task dropForeignTable(type: Exec) {
    // PostgreSQL データベースから fdw_sample 外部テーブルを削除するタスク
    commandLine 'sh', '-c', 'psql postgres -c "drop foreign table if exists fdw_sample"'
}

tasks.named('run') {
    // run タスクの設定: Spring アプリケーションを実行するタスク
    dependsOn createTable           // createTable タスクに依存: run タスクの実行前に createTable タスクを実行
    finalizedBy dropForeignTable    // dropForeignTable タスクをファイナライズ: run タスクの完了後に dropForeignTable タスクを実行
}

createTable.finalizedBy showTable  // createTable タスクのファイナライズ: createTable タスクの完了後に showTable タスクを実行
showTable.finalizedBy createForeignTable  // showTable タスクのファイナライズ: showTable タスクの完了後に createForeignTable タスクを実行
createForeignTable.finalizedBy showForeignTable // createForeignTable タスクのファイナライズ: createForeignTable タスクの完了後に showForeignTable タスクを実行
dropForeignTable.finalizedBy dropTable      // dropForeignTable タスクのファイナライズ: dropForeignTable タスクの完了後に dropTable タスクを実行
